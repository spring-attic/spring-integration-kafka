buildscript {
	repositories {
		maven { url 'https://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath 'io.spring.gradle:spring-io-plugin:0.0.4.RELEASE'
	}
}

description = 'Spring Kafka Support'

apply plugin: 'java'
apply from: "${rootProject.projectDir}/publish-maven.gradle"
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'

if (project.hasProperty('platformVersion')) {
	apply plugin: 'spring-io'

	dependencyManagement {
		springIoTestRuntime {
			imports {
				mavenBom "io.spring.platform:platform-bom:${platformVersion}"
			}
		}
	}
}

group = 'org.springframework.kafka'

repositories {
	maven {
		url 'https://repository.apache.org/content/groups/public'
	}
	maven { url 'http://repo.spring.io/libs-milestone' }
	if (project.hasProperty('platformVersion')) {
		maven { url 'https://repo.spring.io/snapshot' }
	}
}

sourceCompatibility = targetCompatibility = 1.7

ext {
	avroVersion = '1.7.6'
	gsCollectionsVersion = '5.0.0'
	hamcrestVersion = '1.3'
	junitVersion = '4.11'
	kafkaVersion = '0.9.0.1'
	log4jVersion = '1.2.17'
//	metricsVersion = '2.2.0'
	scalaVersion = '2.11'
	reactor2Version = '2.0.6.RELEASE'
	springRetryVersion = '1.1.2.RELEASE'
	springVersion = '4.2.4.RELEASE'

	idPrefix = 'kafka'

	linkHomepage = 'https://github.com/spring-projects/spring-kafka'
	linkCi = 'https://build.spring.io/browse/KAFKA'
	linkIssue = 'https://github.com/spring-projects/spring-integration-kafka/issues'
	linkScmUrl = 'https://github.com/spring-projects/spring-integration-kafka'
	linkScmConnection = 'https://github.com/spring-projects/spring-integration-kafka.git'
	linkScmDevConnection = 'git@github.com:spring-projects/spring-integration-kafka.git'

}

eclipse.project.natures += 'org.springframework.ide.eclipse.core.springnature'

jacoco {
	toolVersion = "0.7.2.201409121644"
}

sourceSets {
	test {
		resources {
			srcDirs = ['src/test/resources', 'src/test/java']
		}
	}
}

dependencies {
	compile "org.springframework:spring-context:$springVersion"
	compile ("org.apache.avro:avro:$avroVersion", optional)
	compile ("org.apache.avro:avro-compiler:$avroVersion", optional)
	compile "com.goldmansachs:gs-collections:$gsCollectionsVersion"
	compile "io.projectreactor:reactor-core:$reactor2Version"

	compile "org.apache.kafka:kafka-clients:$kafkaVersion"

//	runtime "com.yammer.metrics:metrics-core:$metricsVersion"
//	runtime "com.yammer.metrics:metrics-annotation:$metricsVersion"

	testCompile "org.springframework:spring-test:$springVersion"
	testCompile "org.springframework.retry:spring-retry:$springRetryVersion"
	testCompile "org.apache.kafka:kafka_$scalaVersion:$kafkaVersion"
	testCompile "org.apache.kafka:kafka_$scalaVersion:$kafkaVersion:test"

	testCompile ("junit:junit:$junitVersion") {
		exclude group: 'org.hamcrest'
	}
	testCompile "log4j:log4j:$log4jVersion"
	testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"

}

// enable all compiler warnings; individual projects may customize further
[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all,-options']

test {
	// suppress all console output during testing unless running `gradle -i`
	logging.captureStandardOutput(LogLevel.INFO)
	maxHeapSize = "1024m"
	jacoco {
		append = false
		destinationFile = file("$buildDir/jacoco.exec")
	}
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination "${buildDir}/reports/jacoco/html"
	}
}

build.dependsOn jacocoTestReport

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

apply plugin: 'sonar-runner'

sonarRunner {
	sonarProperties {
		property "sonar.jacoco.reportPath", "${buildDir.name}/jacoco.exec"
		property "sonar.links.homepage", linkHomepage
		property "sonar.links.ci", linkCi
		property "sonar.links.issue", linkIssue
		property "sonar.links.scm", linkScmUrl
		property "sonar.links.scm_dev", linkScmDevConnection
		property "sonar.java.coveragePlugin", "jacoco"
	}
}

task api(type: Javadoc) {
	group = 'Documentation'
	description = 'Generates the Javadoc API documentation.'
	title = "${rootProject.description} ${version} API"
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = rootProject.description
	options.overview = 'src/api/overview.html'

	source = sourceSets.main.allJava
	classpath = project.sourceSets.main.compileClasspath
	destinationDir = new File(buildDir, "api")
}

/*
task schemaZip(type: Zip) {
	group = 'Distribution'
	classifier = 'schema'
	description = "Builds -${classifier} archive containing all " +
			"XSDs for deployment at static.springframework.org/schema."

	def Properties schemas = new Properties();
	def shortName = idPrefix.replaceFirst("${idPrefix}-", '')

	project.sourceSets.main.resources.find {
		it.path.endsWith("META-INF${File.separator}spring.schemas")
	}?.withInputStream { schemas.load(it) }

//	for (def key : schemas.keySet()) {
//		File xsdFile = project.sourceSets.main.resources.find {
//			it.path.replaceAll('\\\\', '/').endsWith(schemas.get(key))
//		}
//		assert xsdFile != null
//		into ("integration/${shortName}") {
//			from xsdFile.path
//		}
//	}

}
*/

task docsZip(type: Zip) {
	group = 'Distribution'
	classifier = 'docs'
	description = "Builds -${classifier} archive containing api " +
			"for deployment at static.spring.io/spring-integration/docs."

	from('src/dist') {
		include 'changelog.txt'
	}

	from(api) {
		into 'api'
	}
}

task distZip(type: Zip, dependsOn: [docsZip]) { //, schemaZip]) {
	group = 'Distribution'
	classifier = 'dist'
	description = "Builds -${classifier} archive, containing all jars and docs, " +
			"suitable for community download page."

	ext.baseDir = "${project.name}-${project.version}";

	from('src/dist') {
		include 'readme.txt'
		include 'license.txt'
		include 'notice.txt'
		into "${baseDir}"
	}

	from(zipTree(docsZip.archivePath)) {
		into "${baseDir}/docs"
	}

/*
	from(zipTree(schemaZip.archivePath)) {
		into "${baseDir}/schema"
	}
*/

	into("${baseDir}/libs") {
		from project.jar
		from project.sourcesJar
		from project.javadocJar
	}
}

/*
// Create an optional "with dependencies" distribution.
// Not published by default; only for use when building from source.
task depsZip(type: Zip, dependsOn: distZip) { zipTask ->
	group = 'Distribution'
	classifier = 'dist-with-deps'
	description = "Builds -${classifier} archive, containing everything " +
			"in the -${distZip.classifier} archive plus all dependencies."

	from zipTree(distZip.archivePath)

	gradle.taskGraph.whenReady { taskGraph ->
		if (taskGraph.hasTask(":${zipTask.name}")) {
			def projectName = rootProject.name
			def artifacts = new HashSet()

			rootProject.configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
				def dependency = artifact.moduleVersion.id
				if (!projectName.equals(dependency.name)) {
					artifacts << artifact.file
				}
			}

			zipTask.from(artifacts) {
				into "${distZip.baseDir}/deps"
			}
		}
	}
}
*/

artifacts {
	archives distZip
	archives docsZip
//	archives schemaZip
}

task dist(dependsOn: assemble) {
	group = 'Distribution'
	description = 'Builds -dist, -docs distribution archives.' // and -schema
}

task wrapper(type: Wrapper) {
	description = 'Generates gradlew[.bat] scripts'
	gradleVersion = '2.5'
	distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
